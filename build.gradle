buildscript {
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
    }
    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.10'
}
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'mobi.waterdog.kgeojson'

bintray {
    user = project.properties["bintrayUser"]?.toString() ?: System.getenv("BINTRAY_USER")
    key = project.properties["bintrayApiKey"]?.toString() ?: System.getenv("BINTRAY_API_KEY")
    publications = ["ArtifactPublication"]
    publish = true

    pkg {
        repo = "kgeojson"
        name = "kgeojson"
        licenses = ["MIT"]
        vcsUrl = "https://github.com/waterdog-oss/kgeojson.git"
        userOrg = "waterdog"
        version {
            name = project.version
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        ArtifactPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId "kgeojson"
            version project.version
        }
    }
}

// Junit 5 setup
tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.amshove.kluent:kluent:1.61"

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
}
